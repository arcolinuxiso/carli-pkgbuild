# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# https://gitlab.manjaro.org/manjaro-arm/packages/extra/calamares/-/blob/master/PKGBUILD
# https://gitlab.manjaro.org/packages/extra/calamares/-/blob/master/PKGBUILD

pkgname=calamares
# do not forget the commit
pkgver=3.3.13.241225
pkgrel=01
commit=935a935
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares/calamares/"
license=('LGPL')
provides=('calamares')
conflicts=("calamaresq")
depends=(
  'boost-libs'
  'ckbcomp'
  'cryptsetup'
  'doxygen'
  'efibootmgr'
  'gptfdisk'
  'gtk-update-icon-cache'
  'hwinfo'
  'icu'
  'kconfig'
  'kcoreaddons'
  'kcrash'
  'ki18n'
  'kparts'
  'kpmcore'
  'kservice'
  'kwidgetsaddons'
  'libpwquality'
  'mkinitcpio-openswap'
  'polkit-qt6'
  'rsync'
  'qt6-declarative'
  'solid'
  'squashfs-tools'
  'yaml-cpp'
)
makedepends=(
  'boost'
  'cmake'
  'extra-cmake-modules'
  'git'
  'ninja'
  'python-jsonschema'
  'python-pyaml'
  'python-unidecode'
  'qt6-tools'
)
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source=($pkgname::git+https://github.com/calamares/calamares#commit=$commit)
sha256sums=('681092d5f63f3d1e8f3d8ffa05703e4902c605dcc62dc20477718b44522b4b35')

prepare() {
	cd ${srcdir}/calamares
	sed -i -e 's/etc\/sddm.conf/etc\/sddm.conf.d\/kde_settings.conf/' src/modules/displaymanager/main.py
}

build() {
	cd ${srcdir}/calamares

  cmake -S . -Bbuild \
      -GNinja \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DWITH_APPSTREAM=OFF \
      -DWITH_PYBIND11=OFF \
      -DWITH_QT6=ON \
      -DSKIP_MODULES="dracut \
          dracutlukscfg \
          dummycpp \
          dummyprocess \
          dummypython \
          dummypythonqt \
          initramfs \
          initramfscfg \
          interactiveterminal \
          keyboardq \
          license \
          localeq \
          oemid \
          packagechooserq \
          partitionq \
          services-openrc \
          summaryq \
          tracking \
          usersq \
          welcomeq \
          zfs \
          zfshostid"

  cmake --build build
}

package() {
	cd ${srcdir}/calamares/build
	DESTDIR="${pkgdir}" cmake --build . --target install
}
